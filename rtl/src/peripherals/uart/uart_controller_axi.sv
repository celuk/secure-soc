// This file is part of https://github.com/celuk/secure-soc
// Copyright (C) 2025  Seyyid Hikmet Celik
//                     seyyid4091@gmail.com
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// uart_controller_axi.sv
`timescale 1ns / 1ps

import axi_pkg::*;

module uart_controller_axi #(
    parameter int unsigned AXI_ID_WIDTH   = 4,
    parameter int unsigned AXI_ADDR_WIDTH = 32,
    parameter int unsigned AXI_DATA_WIDTH = 32,
    parameter int unsigned WB_ADDR_WIDTH  = 8
) (
    input  logic clk_i,
    input  logic rst_ni,

    input  logic                            s_axi_awvalid,
    output logic                            s_axi_awready,
    input  logic [AXI_ADDR_WIDTH-1:0]       s_axi_awaddr,
    input  logic [AXI_ID_WIDTH-1:0]         s_axi_awid,
    input  logic [2:0]                      s_axi_awprot,
    input  logic                            s_axi_wvalid,
    output logic                            s_axi_wready,
    input  logic [AXI_DATA_WIDTH-1:0]       s_axi_wdata,
    input  logic [AXI_DATA_WIDTH/8-1:0]     s_axi_wstrb,
    output logic                            s_axi_bvalid,
    input  logic                            s_axi_bready,
    output logic [AXI_ID_WIDTH-1:0]         s_axi_bid,
    output logic [1:0]                      s_axi_bresp,
    input  logic                            s_axi_arvalid,
    output logic                            s_axi_arready,
    input  logic [AXI_ADDR_WIDTH-1:0]       s_axi_araddr,
    input  logic [AXI_ID_WIDTH-1:0]         s_axi_arid,
    input  logic [2:0]                      s_axi_arprot,
    output logic                            s_axi_rvalid,
    input  logic                            s_axi_rready,
    output logic [AXI_ID_WIDTH-1:0]         s_axi_rid,
    output logic [AXI_DATA_WIDTH-1:0]       s_axi_rdata,
    output logic [1:0]                      s_axi_rresp,

    input  logic rx_i,
    output logic tx_o
);

    localparam DATA_BYTES = AXI_DATA_WIDTH / 8;

    typedef enum logic [2:0] {
        S_IDLE, S_WRITE_ADDR, S_WRITE_DATA, S_READ_ADDR, S_WAIT_ACK, S_RESP
    } state_e;

    state_e current_state, next_state;

    logic                            wb_cyc;
    logic                            wb_stb;
    logic                            wb_we;
    logic [AXI_ADDR_WIDTH-1:0]       wb_adr_reg;
    logic [AXI_DATA_WIDTH-1:0]       wb_dat_w_reg;
    logic [DATA_BYTES-1:0]           wb_sel_reg;
    logic                            wb_ack;
    logic [AXI_DATA_WIDTH-1:0]       wb_dat_r;

    logic [AXI_DATA_WIDTH-1:0]       reg_axi_rdata;
    logic                            reg_is_write;
    logic [AXI_ID_WIDTH-1:0]         reg_axi_id;

    uart_controller uart_iface_dut (
       .clk_i(clk_i), .rst_i(~rst_ni), .wb_adr_i(wb_adr_reg[WB_ADDR_WIDTH-1:0]),
       .wb_dat_i(wb_dat_w_reg), .wb_we_i(wb_we), .wb_stb_i(wb_stb),
       .wb_sel_i(wb_sel_reg), .wb_cyc_i(wb_cyc), .wb_ack_o(wb_ack),
       .wb_dat_o(wb_dat_r), .uart_rx_i(rx_i), .uart_tx_o(tx_o)
    );

    assign s_axi_awready = (current_state == S_IDLE);
    assign s_axi_wready  = (current_state == S_WRITE_ADDR);
    assign s_axi_arready = (current_state == S_IDLE);

    assign s_axi_bvalid = (current_state == S_RESP) && reg_is_write;
    assign s_axi_bresp  = RESP_OKAY;
    assign s_axi_bid    = reg_axi_id;

    assign s_axi_rvalid = (current_state == S_RESP) && !reg_is_write;
    assign s_axi_rdata  = reg_axi_rdata;
    assign s_axi_rresp  = RESP_OKAY;
    assign s_axi_rid    = reg_axi_id;

    assign wb_cyc = (current_state == S_WRITE_DATA) || (current_state == S_READ_ADDR) || (current_state == S_WAIT_ACK);
    assign wb_stb = wb_cyc;
    assign wb_we  = (current_state == S_WRITE_DATA) || ((current_state == S_WAIT_ACK) && reg_is_write);

    always_ff @(posedge clk_i or negedge rst_ni) begin
        if (!rst_ni) current_state <= S_IDLE; else current_state <= next_state; end

    always_ff @(posedge clk_i or negedge rst_ni) begin
        if (!rst_ni) begin
            wb_adr_reg   <= '0; wb_dat_w_reg <= '0; wb_sel_reg   <= '0;
            reg_axi_rdata<= '0; reg_is_write <= 1'b0; reg_axi_id <= '0;
        end else begin
            if (s_axi_awvalid && s_axi_awready) begin
                wb_adr_reg <= s_axi_awaddr;
                reg_is_write <= 1'b1;
                reg_axi_id <= s_axi_awid;
            end else if (s_axi_arvalid && s_axi_arready) begin
                wb_adr_reg <= s_axi_araddr;
                reg_is_write <= 1'b0;
                reg_axi_id <= s_axi_arid;
            end

            if (s_axi_wvalid && s_axi_wready) begin
                wb_dat_w_reg <= s_axi_wdata;
                wb_sel_reg   <= s_axi_wstrb;
            end

            if (current_state == S_WAIT_ACK && wb_ack && !reg_is_write) begin
                reg_axi_rdata <= wb_dat_r; end

            if (s_axi_rvalid && s_axi_rready) begin reg_axi_rdata <= '0; end
        end
    end

    always_comb begin
        next_state = current_state;
        case (current_state)
            S_IDLE: begin
                if (s_axi_awvalid) begin next_state = S_WRITE_ADDR;
                end else if (s_axi_arvalid) begin next_state = S_READ_ADDR; end
            end
            S_WRITE_ADDR: begin if (s_axi_wvalid) begin next_state = S_WRITE_DATA; end end
            S_WRITE_DATA: begin next_state = S_WAIT_ACK; end
            S_READ_ADDR:  begin next_state = S_WAIT_ACK; end
            S_WAIT_ACK: begin if (wb_ack) begin next_state = S_RESP; end end
            S_RESP: begin
                if (reg_is_write && s_axi_bready) begin next_state = S_IDLE;
                end else if (!reg_is_write && s_axi_rready) begin next_state = S_IDLE; end
            end
            default: next_state = S_IDLE;
        endcase
    end

endmodule